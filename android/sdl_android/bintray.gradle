apply plugin: "com.jfrog.bintray"
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'maven-publish'

def siteUrl = 'https://github.com/smartdevicelink/sdl_java_suite'      // Homepage URL of the library
def gitUrl = 'https://github.com/smartdevicelink/sdl_java_suite.git'   // Git repository URL
group = "com.smartdevicelink" // Maven Group ID for the artifact
def libDescription = 'SmartDeviceLink mobile library for Android'
def libVersion = new File(projectDir.path, ('/../../VERSION')).text.trim()


install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'

                // Add your description here
                name 'com.smartdevicelink:sdl_android'
                description = libDescription
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'BSD 3-Clause'
                        url 'https://opensource.org/licenses/BSD-3-Clause'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

bintray {
    Properties props = new Properties()
    props.load(new FileInputStream("$projectDir/bintray.properties"))

    // Authorization
    user = props.getProperty("bintray.user")
    key = props.getProperty("bintray.key")
    version = libVersion
    configurations = ['archives']
    pkg {
        repo = props.getProperty("bintray.repo")
        name = props.getProperty("bintray.artifact")
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        userOrg = props.getProperty("bintray.userorg")
        licenses = ["BSD 3-Clause"]
        publish = props.getProperty("bintray.publish")  // Will upload to jCenter
        version {
            name = libVersion // Change to release version
            desc = libDescription
            released  = new Date()  // Will be the current date & time
            vcsTag = libVersion // Should match git tag
        }
    }
}